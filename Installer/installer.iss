; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!


#ifndef MyAppVersion
  #define MyAppVersion "3.10.6"
  #define MyAppName "Tooll-v3.10.6"
#endif
#define MyAppPublisher "t3"
#define MyAppURL "https://www.tooll.io//"
#define MyAppExeName "Editor.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId=t3.tooll
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=c:\Tooll\{#MyAppName}
; "ArchitecturesAllowed=x64compatible" specifies that Setup cannot run
; on anything but x64 and Windows 11 on Arm.
ArchitecturesAllowed=x64compatible
; "ArchitecturesInstallIn64BitMode=x64compatible" requests that the
; install be done in "64-bit mode" on x64 or Windows 11 on Arm,
; meaning it should use the native 64-bit Program Files directory and
; the 64-bit view of the registry.
ArchitecturesInstallIn64BitMode=x64compatible
DisableProgramGroupPage=yes

; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest

OutputDir=.\Output
OutputBaseFilename=Tooll-v{#MyAppVersion}
SetupIconFile=..\Resources\images\editor\t3.ico 
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\Editor\bin\Release\net9.0-windows\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs
Source: "dependencies\downloads\dotnet-sdk-9.0.102-win-x64.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
Source: "dependencies\downloads\VC_redist.x64.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
Source: "dependencies\grafiktools.bat"; DestDir: "{tmp}"; Flags: deleteafterinstall ignoreversion

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Code]
function GetDotNetVersion: Boolean;
begin
  Result := FileExists(ExpandConstant('{pf}\dotnet\sdk\9.0.102\dotnet.runtimeconfig.json'));
  
end;

function GetvcruntimeVersion: Boolean;
begin
  Result := FileExists('C:\Windows\System32\vcruntime140.dll');
end;

[Run]
Filename: "{tmp}\dotnet-sdk-9.0.102-win-x64.exe"; StatusMsg: "Install .Net9"; Check: not GetDotNetVersion
Filename: "{tmp}\VC_redist.x64.exe"; StatusMsg: "Install Visual C++ Redistributable"; check: not GetvcruntimeVersion 
Filename: "{tmp}\grafiktools.bat"; StatusMsg: "Start Windows Graphic Tools. This can take up to 10 minutes."; Flags: runhidden 
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent